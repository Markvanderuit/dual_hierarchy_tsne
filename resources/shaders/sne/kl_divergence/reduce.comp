/*
 * MIT License
 *
 * Copyright (c) 2021 Mark van de Ruit (Delft University of Technology)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

#version 460 core

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

// Buffer bindings
layout(binding = 0, std430) restrict readonly buffer Input { float inputBuffer[]; };
layout(binding = 1, std430) restrict buffer Intermediate { float intermediateBuffer[256]; };
layout(binding = 2, std430) restrict writeonly buffer Final { float finalBuffer; };

// Uniform locations
layout(location = 0) uniform uint nPoints;
layout(location = 1) uniform uint iter;

const uint groupSize = gl_WorkGroupSize.x;
const uint halfGroupSize = groupSize / 2;
shared float reductionArray[halfGroupSize];

void main() {
  uint lid = gl_LocalInvocationID.x;
  float sum = 0.f;
  if (iter == 0) {
    // First iteration reduces n to 128
    for (uint i = gl_WorkGroupID.x * gl_WorkGroupSize.x + lid;
        i < nPoints;
        i += gl_WorkGroupSize.x * gl_NumWorkGroups.x) {
      sum += inputBuffer[i];
    }
  } else if (iter == 1) {
    // Second iteration reduces 128 to 1
    sum = intermediateBuffer[lid];      
  }

  // Reduce add to a single value
  if (lid >= halfGroupSize) {
    reductionArray[lid - halfGroupSize] = sum;
  }
  barrier();
  if (lid < halfGroupSize) {
    reductionArray[lid] += sum;
  }
  for (uint i = halfGroupSize / 2; i > 1; i /= 2) {
    barrier();
    if (lid < i) {
      reductionArray[lid] += reductionArray[lid + i];
    }
  }
  barrier();
  if (lid < 1) {
    if (iter == 0) {
      intermediateBuffer[gl_WorkGroupID.x] = reductionArray[0] + reductionArray[1];
    } else if (iter == 1) {
      finalBuffer = reductionArray[0] + reductionArray[1];
    }
  }
}